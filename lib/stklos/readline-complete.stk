;;;;
;;;; readline-complete.stk   -- tab completion for readline
;;;;
;;;; Copyright © 2022 Jeronimo Pellegrini <j_p@aleph0.info>
;;;;
;;;;
;;;; This program is free software; you can redistribute it and/or modify
;;;; it under the terms of the GNU General Public License as published by
;;;; the Free Software Foundation; either version 2 of the License, or
;;;; (at your option) any later version.
;;;;
;;;; This program is distributed in the hope that it will be useful,
;;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;;; GNU General Public License for more details.
;;;;
;;;; You should have received a copy of the GNU General Public License
;;;; along with this program; if not, write to the Free Software
;;;; Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307,
;;;; USA.
;;;;           Author: Jerônimo Pellegrini [j_p@aleph0.info]
;;;;    Creation date: 08-May-2022 08:59
;;;; Last file update: 30-May-2022 16:32 (eg)

(select-module stklos/readline-complete)

(export completer-function            ; parametee to get/set clompletion function
        default-complete-function     ; the prefix default completion function
        substring-complete-function)  ; another compeletion function using substring 


(define (get-module-symbols module)
  ;; Find all the global symbols of a module/library
  (append (module-symbols (current-module))
          (if (library? module)
              '()
              (module-symbols (find-module 'STklos)))))


;;
;; A default completer function, which completes on the symbols usable
;; at the global level of the current REPL module. 
;;
(define (default-complete-function str)
  (if (zero? (string-length str))
      '()
      ;; We just compare the prefix.
      (let* ((symbols (get-module-symbols (current-module)))
             (size (string-length str))
             (match (lambda (s)
                      (and (>= (string-length s) size)
                           (string=? str (substring s 0 size))))))
        (filter match (map symbol->string symbols)))))))

;;
;; Another completer. This one tries to find if str is a substring of
;; a symbols defined at global level
;;
(define (substring-complete-function str)
  (if (zero? (string-length str))
      '()
      (let ((symbols (get-module-symbols (current-module)))
            (match (lambda (s) (string-find? str s))))
        (filter match (map symbol->string symbols)))))


;;
;; The parameter completer-function contains the function used to complete strings
;; It defaults to default-complet-function
;;
(define completer-function (make-parameter default-complete-function))


;; readline-completion-generator is a generator of string completions.
;; It takes as argument a string, 'str' and a state indicator, 'state'.
;;
;; - Completions will be searched for 'str'.
;; - When state is #f a fresh list of completions is searched, and the first
;; item is returned.
;; - When state is #t the next of the current list is returned.
;;
;; This is written this way to match the expected behavior of the readline
;; function rl_completion_matches.
(define readline-completion-generator
  (let ((ans '()))
    (lambda (str state)
      (when (not state)
        (set! ans ((completer-function) str)))
      (if (null? ans)
          ans
          (let ((s (car ans)))
            (set! ans (cdr ans))
            s)))))

;;
;; Initialization
;;
(unless (eq? (key-get *%system-state-plist* :readline 'no-readline)
             'no-readline)
  ;; Tell readline to start doing tab completion, by passing our completion
  ;; function to it.
  (%init-readline-completion-function readline-completion-generator))

(provide "stklos/readline-complete")
